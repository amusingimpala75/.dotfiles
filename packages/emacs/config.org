#+PROPERTY: header-args:emacs-lisp :tangle yes

* My Emacs Configuration

Lexical Bindings, and ensure we can use :bind:
#+begin_src emacs-lisp
;;; -*- lexical-binding: t -*-
(require 'bind-key)
(require 'nix-settings)

(define-prefix-command 'my/applications nil "Applications")
(bind-key "C-x C-a" 'my/applications)

(defun my/user-secret-else (file else)
  (if (file-exists-p file)
      (with-temp-buffer
        (insert-file-contents file)
        (read (current-buffer)))
    else))

;; (use-package benchmark-init
;;   :ensure t
;;   :demand t
;;   :hook (after-init . benchmark-init/deactivate)
;;   :init
;;   (benchmark-init/activate))
#+end_src

** UI
I really don't want the scroll bar or the tool bar, nor is the startup screen necessary; additionally the menu bar is only really fine on macOS where it is separate from the window, and hidden most of the time.
#+begin_src emacs-lisp
(use-package scroll-bar
  :custom (scroll-bar-mode nil)) ;;  Also see the section regarding frame defaults
(use-package tool-bar
  :custom (tool-bar-mode nil))

(setq initial-buffer-choice t)

(use-package emacs
  :custom
  (menu-bar-mode (if (eq system-type 'darwin)
                     t
                   nil)))

(use-package xt-mouse
  :config
  (add-hook 'after-make-frame-functions (lambda (_)
                                          (xterm-mouse-mode t)))
  (add-hook 'delete-frame-functions (lambda (_)
                                      (xterm-mouse-mode -1))))
#+end_src

Show a line at 80 chars (this helps code format) and show the dimensions of boundaries.
#+begin_src emacs-lisp
(use-package emacs
  :custom
  (fill-column 80)
  (indicate-buffer-boundaries 'left)
  :hook
  (prog-mode . display-fill-column-indicator-mode))
#+end_src

In order to declutter the bottom, I need diminish
#+begin_src emacs-lisp
(use-package diminish
  :ensure t)
#+end_src

~which-key~ being integrated into core is great for discovery.
#+begin_src emacs-lisp
(use-package which-key
  :diminish which-key-mode
  :custom (which-key-mode t))
#+end_src

The normal bell noise is just awful, this replaces it with the visual bell.
#+begin_src emacs-lisp
(use-package emacs
  :custom (visible-bell 1))
#+end_src

I like to be able to visualize colors in the editor, but I probably don't want it on always
#+begin_src emacs-lisp
(use-package rainbow-mode
  :ensure t)
#+end_src

Breadcrumbs to show where I am
#+begin_src emacs-lisp
(use-package breadcrumb
  :ensure t
  :custom (breadcrumb-mode t))
#+end_src

*** Scrolling
The frame really needs to scroll better.
#+begin_src emacs-lisp
(use-package emacs
  :custom
  ;(scroll-margin 2)
  (scroll-conservatively 101)
  (pixel-scroll-precision-mode t)
  (frame-resize-pixelwise t)
  (scroll-preserve-screen-position t)
  (scroll-error-top-bottom t))
#+end_src

Additionally, truncate text off the edge of the screen unless in a text mode
#+begin_src emacs-lisp
(use-package emacs
  :custom (truncate-lines t))
(use-package text-mode
  :diminish visual-line-mode
  :hook (text-mode . visual-line-mode))
#+end_src

*** Theme
Theming is controlled by nix-settings, often base16 generated
#+begin_src emacs-lisp
;; Snippet moved into nix-settings.el as part of the system config
#+end_src

Just to be sure, we also install the ef-themes
#+begin_src emacs-lisp
(use-package ef-themes
  :ensure t
  :demand t
  :defer nil
  :custom
  (ef-themes-mixed-fonts t)
  (ef-themes-to-toggle '(ef-elea-light ef-elea-dark))
  (ef-themes-common-palette-overrides '((bg-region bg-main)))
  :hook
  (ef-themes-after-load-theme . (lambda ()
                                  (set-face-attribute 'region nil :inverse-video t)
                                  (set-face-attribute 'region nil :extend nil))))
#+end_src

I also like a transparent background (still waiting on alpha-background on mac)
#+begin_src emacs-lisp
;; Update if macOS ever supports alpha-background
(defvar my/alpha-setting (if (string-match-p "PGTK" system-configuration-features)
                             `(alpha-background . ,(max 0 (- my/opacity 5)))
                           `(alpha . ,(min 100 (+ my/opacity 5)))))
(use-package frame
  :custom
  (default-frame-alist `(,my/alpha-setting
                         (ns-transparent-titlebar . t)
			 (vertical-scroll-bars . nil)
			 (horizontal-scroll-bars . nil))))
#+end_src

*** Font
Pull from the user configuration for the font family/size
#+begin_src emacs-lisp
(use-package cus-face
  :custom-face
  (default ((t (:family ,my/font-family-fixed-pitch :height ,(* my/font-size 10)))))
  (variable-pitch ((t (:family ,my/font-family-variable-pitch))))
  (fixed-pitch ((t (:family ,my/font-family-fixed-pitch))))
  :config
  ;; I wish I could just do it for the single character required ¯\_(ツ)_/¯
  ;; But org-export or nix or something is not happy (unrecognized token)
  (unless (eq 'system-type 'darwin)
    (set-fontset-font t nil "Noto Sans CJK" nil 'append)))
#+end_src

#+begin_src emacs-lisp
(use-package ligature
  :ensure t
  :custom
  (global-ligature-mode t)
  :config
  (ligature-set-ligatures 'prog-mode '("==" "!=" ">=" "<=" "->" "=>"
                                       ".." "..." "++" "+=" "::=" "__"
                                       "===" "!==")))
#+end_src

*** Dashboard
Use the dashboard for a nice splash screen
#+begin_src emacs-lisp
(use-package dashboard
  :ensure t
  :custom
  (dashboard-center-content t)
  :hook
  (after-init-hook . dashboard-refresh-buffer)
  :bind
  ("C-x C-a d" . dashboard-refresh-buffer)
  (:map dashboard-mode-map
   ("n" . dashboard-next-line)
   ("p" . dashboard-previous-line))
  :config
  (dashboard-setup-startup-hook)
  (setq initial-buffer-choice (lambda () (dashboard-refresh-buffer))))
#+end_src

Use the biblegateway VotD for the footer
#+begin_src emacs-lisp
(use-package bible-gateway
  :ensure t
  :after dashboard
  :custom
  (dashboard-footer-messages (list (bible-gateway-get-verse))))
#+end_src

** LSP, DAP, Treesitter, Flymake
Fixes stupid bug where the line height changes ever-so-slightly on macOS
#+begin_src emacs-lisp
(use-package eglot
  :defer t
  :functions eglot-completion-at-point
  :custom
  (eglot-code-action-indicator "*")
  (eglot-semantic-tokens-mode t)
  :hook
  ((c-ts-mode fennel-mode go-ts-mode haskell-mode nix-mode java-ts-mode js-ts-mode lua-ts-mode rustic-mode scala-mode) . eglot-ensure))
#+end_src

#+begin_src emacs-lisp
(use-package dape
  :ensure t
  :defer t)
#+end_src

Configure treesitter to use maximum font locking
#+begin_src emacs-lisp
(use-package treesit
  :custom
  (treesit-font-lock-level 4)
  (treesit-enabled-modes t))
#+end_src

Inline diagnostics is helpful
#+begin_src emacs-lisp
(use-package flymake
  :defer t
  :custom
  (flymake-show-diagnostics-at-end-of-line t))
#+end_src

** Language Support (modes)
Make sure skeleton stuff is available
#+begin_src emacs-lisp
(use-package skeleton
  :defines skeleton-positions)
#+end_src

*** ~fundamental-mode~ (global)
**** Snippets
***** Indent snippet
#+begin_src emacs-lisp
(defun my/skeleton-indent-afterwards ()
  (indent-region (car (last skeleton-positions)) (car skeleton-positions)))
#+end_src

***** Insert current file name
#+begin_src emacs-lisp
(define-skeleton fundamental-skeleton-current-file
  "Insert the name of the current file"
  nil ;; no prompt
  > (file-name-nondirectory (buffer-file-name)))
#+END_src

***** Insert current date
#+begin_src emacs-lisp
(define-skeleton fundamental-skeleton-current-date
  "Insert the date"
  nil ;; no prompt
  > (format-time-string "%Y-%m-%d"))
#+end_src

***** Fancy heading
#+begin_src emacs-lisp
(defun my/longest-line (str)
  "Return length of longest single line in `str'."
  (seq-max (mapcar 'string-width (split-string str "\n"))))

(define-skeleton fundamental-skeleton-heading
  "Generate surrounded heading"
  ""
  '(setq str (skeleton-read "Title: "))
  > (make-string (my/longest-line str) ?=) \n
  > str \n
  > (make-string (my/longest-line str) ?=))
#+end_src

**** Config
Actually add the snippets
#+begin_src emacs-lisp
(defun my/add-skeleton-abbrevs (table pairs)
  (dolist (pair pairs)
    (let ((name (car pair))
          (fn (cdr pair)))
      (define-abbrev table name "" fn 1))))

(defun my/add-fundamental-snippets (table)
  (define-abbrev table "shr" "¯\\_(ツ)_/¯" nil 1)
  (my/add-skeleton-abbrevs table '(("here" . fundamental-skeleton-current-file)
                                   ("now" . fundamental-skeleton-current-date)
                                   ("heading" . fundamental-skeleton-heading))))
#+end_src

#+begin_src emacs-lisp
(use-package lisp
  :bind ("M-\"" . my/insert-quote)
  :init
  (defun my/insert-quote (&optional arg)
    (interactive "P")
    (insert-pair arg ?\" ?\")))
#+end_src

*** Markdown
While many READMEs are in Markdown, this is actually here so that Eldoc (and thus corfu-popupinfo) renders docstrings correctly
#+begin_src emacs-lisp
(use-package markdown-mode
  :ensure t
  :mode ("README\\.md\\'" . gfm-mode)
  :config
  (my/add-fundamental-snippets markdown-mode-abbrev-table))
#+end_src

*** Nix
Since this is an emacs configuration after all, we use nix-mode
#+begin_src emacs-lisp
(use-package nix-mode
  :ensure t
  :config
  (with-eval-after-load 'eglot
    (add-to-list 'eglot-server-programs '(nix-mode . ("nixd"))))
  (my/add-fundamental-snippets nix-mode-abbrev-table))
#+end_src

*** \LaTeX
\LaTeX is a necessary component for document editing
**** Snippets
#+begin_src emacs-lisp
(define-skeleton LaTeX-skeleton-begin
  "Insert begin and end tags"
  '(setq str (skeleton-read "Type: "))
  > "\\begin{" str "}" \n
  > \n
  > "\\end{" str "}")

(defun my/add-LaTeX-snippets (table)
  (my/add-fundamental-snippets table)
  (define-abbrev table "ria" "\\rightarrow" nil 1)
  (my/add-skeleton-abbrevs table '(("begin" . LaTeX-skeleton-begin))))
#+end_src

**** config
#+begin_src emacs-lisp
;; I don't like this hackiness, but latex/auctex/tex loading is weird
(use-package latex
  :defines LaTeX-mode-abbrev-table)
(use-package tex
  :ensure auctex
  :custom
  (TeX-auto-save t)
  (TeX-parse-self t)
  (TeX-master t)
  (reftex-plug-into-AUCTeX t)
  (TeX-save-query nil)
  :hook
  (LaTeX-mode . visual-line-mode)
  (LaTeX-mode . flyspell-mode)
  (LaTeX-mode . LaTeX-math-mode)
  (LaTeX-mode . turn-on-reftex)
  :init
  (defun my/add-latex-preview-save-hook ()
    (add-hook 'after-save-hook 'preview-buffer nil t))
  (add-hook 'LaTeX-mode-hook 'my/add-latex-preview-save-hook)
  :config
  (my/add-LaTeX-snippets LaTeX-mode-abbrev-table))
#+end_src

*** Org Mode
#+begin_src emacs-lisp
(use-package org
  :defer 5
  :diminish org-indent-mode
  :hook
  (org-mode . variable-pitch-mode)
  (org-mode . visual-line-mode)
  (org-mode . (lambda ()
                (setq-local electric-pair-inhibit-predicate
                            (lambda (c)
                              (if (char-equal c ?\<) ;; > just to get this to shut up
                                  t
                                (electric-pair-default-inhibit c))))))
  :custom-face
  (org-block ((t (:inherit fixed-pitch))))
  (org-table ((t (:inherit fixed-pitch))))
  (org-code ((t (:inherit (shadow fixed-pitch)))))
  (org-level-1 ((t (:weight bold :height 1.5))))
  (org-level-2 ((t (:weight bold :height 1.4))))
  (org-level-3 ((t (:weight bold :height 1.3))))
  (org-level-4 ((t (:weight bold :height 1.2))))
  (org-level-5 ((t (:weight bold :height 1.1))))
  (org-level-6 ((t (:weight bold))))
  (org-level-7 ((t (:weight bold))))
  (org-level-8 ((t (:weight bold))))
  :custom
  (org-src-fontify-natively t)
  (org-src-preserve-indentation t)
  (org-startup-indented t)
  (org-hide-emphasis-markers t)
  :functions
  org-beginning-of-line
  org-end-of-line
  org-export-define-derived-backend
  org-export-output-file-name
  org-export-to-file
  org-mark-element
  :config
  (add-to-list 'org-modules 'org-tempo)
  (my/add-LaTeX-snippets org-mode-abbrev-table)
  (org-babel-do-load-languages 'org-babel-load-languages '((python . t)
                                                           (R . t)
                                                           (jupyter . t)
                                                           (shell . t)))
  (defun my/count-words-buffer ()
    (interactive)
    (let ((start (if (region-active-p) (min (mark) (point))
                   (point-min)))
          (end (if (region-active-p) (max (mark) (point))
                 (point-max))))
      (message "%d" (count-words start end nil)))))

(diminish 'buffer-face-mode)

(use-package org-latex-preview
  :defer t
  :after org
  :hook
  (org-mode . org-latex-preview-mode)
  :custom
  (org-latex-preview-mode-display-live t)
  (org-latex-preview-mode-update-delay 0.1)
  :config
  (setcdr (assoc "pdflatex" org-latex-preview-compiler-command-map) (executable-find "latex")))
#+end_src

Org-modern just makes things look nicer
#+begin_src emacs-lisp
(use-package org-modern
  :ensure t
  :custom
  (org-modern-star 'replace)
  :hook (org-mode . org-modern-mode))

(use-package org-modern-indent
  :hook (org-modern-mode . org-modern-indent-mode))
#+end_src

org-export to docx via latex and pandoc
#+begin_src emacs-lisp
(defun my/pandoc-latex-to-docx (texfile &optional _)
  (let ((infile (shell-quote-argument texfile))
        (outfile (shell-quote-argument (format "%s.docx" (substring texfile 0 -4)))))
    (shell-command (format "pandoc %s -o %s" infile outfile))))

(defun my/org-latex-export-to-docx (&optional async subtreep visible-only body-only ext-plist)
  (interactive)
  (let ((outfile (org-export-output-file-name ".tex" subtreep)))
    (org-export-to-file 'latex outfile
      async subtreep visible-only body-only ext-plist
      #'my/pandoc-latex-to-docx)))

(with-eval-after-load 'ox-latex
  (org-export-define-derived-backend 'docx 'latex
				     :menu-entry
				     '(?l 1
					  ((?d "As LaTeX file (Docx)" my/org-latex-export-to-docx)))))
#+end_src

Allow jupyter in org files, and easily format an HTML result block
#+begin_src emacs-lisp
(use-package jupyter
  :ensure t)

(defun my/babel-render-export ()
  (interactive)
  (org-mark-element)
  (forward-line 2)
  (org-beginning-of-line)
  (exchange-point-and-mark)
  (forward-line -3)
  (org-end-of-line)
  (shr-render-region (mark) (point)))
#+end_src

*** Java
Configure java to use jdtls/eglot
# TODO make this a (use-package java-mode ...) or (use-package cc-mode ...). I cannot for the life of me get those to work.
**** Snippets
***** Main class generator
#+begin_src emacs-lisp
(define-skeleton java-skeleton-def-main
  "Generate java main class/function."
  ""
  @
  "public class " (capitalize (file-name-nondirectory (file-name-sans-extension (buffer-name)))) " {" \n
  "public static void main(String[] args) {" \n
  _ \n
  "}" \n
  "}"
  @
  '(my/skeleton-indent-afterwards))
#+end_src

produces:

#+begin_src java
public class [Class name from file name] {
    public static void main(String[] args) {
        <cursor here>
    }
}
#+end_src

***** println generator
#+begin_src emacs-lisp
(define-skeleton java-skeleton-println
  "Generate println statement."
  ""
  > "System.out.println(" (skeleton-read "text: ") ");" \n
  > _)
#+end_src

**** Config
#+begin_src emacs-lisp
(defun my/add-java-snippets (table)
  (my/add-fundamental-snippets table)
  (my/add-skeleton-abbrevs table '(("defmain" . java-skeleton-def-main)
                                   ("pr" . java-skeleton-println))))

(use-package java-ts-mode
  :config
  (my/add-java-snippets java-ts-mode-abbrev-table))
#+end_src

*** Scala
#+begin_src emacs-lisp
(use-package scala-mode
  :ensure t
  :hook
  (scala-mode . (lambda ()
                  (setq-local eglot-ignored-server-capabilities '(:documentOnTypeFormattingProvider)))) ; Because it would freeze each time for no reason
  (scala-mode . (lambda ()
                  (setq prettify-symbols-alist scala-prettify-symbols-alist)
                  (prettify-symbols-mode)))
  :config
  (add-to-list 'project-vc-extra-root-markers "build.sbt"))
(use-package sbt-mode
  :ensure t)
#+end_src

*** Rust
#+begin_src emacs-lisp
(use-package rust-mode
  :ensure t
  :custom
  (rust-mode-treesitter-derive t)
  (rust-format-on-save t))
(use-package rustic
  :ensure t
  :after rust-mode
  :custom
  (rustic-lsp-setup-p nil)
  :config
  (my/add-fundamental-snippets rustic-mode-abbrev-table))
#+end_src

*** Lua
#+begin_src emacs-lisp
(use-package lua-ts-mode
  :config
  (my/add-fundamental-snippets lua-ts-mode-abbrev-table))
#+end_src

*** Zig
#+begin_src emacs-lisp
(use-package zig-mode
  :ensure t
  :diminish zig-format-on-save-mode
  :config
  (my/add-fundamental-snippets zig-mode-abbrev-table))
#+end_src

*** C
**** Snippets
***** Header Guard
(yes I know ~#pragma once~ exists)
#+begin_src emacs-lisp
(defun my/default-header-guard-name ()
  "Default header guard created by upcase the file name."
  (concat (upcase (file-name-nondirectory (file-name-sans-extension (buffer-file-name)))) "_H"))

(define-skeleton c-skeleton-guard
  "Generate header guard"
  ""
  '(setq str (skeleton-read "Header: " (my/default-header-guard-name)))
  "#ifndef " str \n
  "#define " str \n
  \n
  _ \n
  \n
  "#endif /* " str " */")
#+end_src

produces

#+begin_src c
#ifndef CONFIG_H
#define CONFIG_H

<cursor here>

#endif /* CONFIG_H */
#+end_src

***** Doxygen Header comment
#+begin_src emacs-lisp
(define-skeleton c-skeleton-doxy-header
  "Doxygen comment for header."
  ""
  @
  "/**" \n
  " * @file " (file-name-nondirectory (buffer-file-name)) \n
  " * @author " (skeleton-read "Author: " nil nil) \n ;; TODO default author
  " * @brief " (skeleton-read "Brief: " nil nil) \n
  " * @date " (format-time-string "%Y-%m-%d") \n
  " */"
  @
  '(my/skeleton-indent-afterwards))
#+end_src

***** Doxygen function comment
#+begin_src emacs-lisp
(define-skeleton c-skeleton-doxy-fn
  "Doxygen comment for a function."
  ""
  @
  "/**" \n
  " * @brief " (skeleton-read "Brief: ") \n
  " *" \n
  " * " (skeleton-read "Description: ") \n
  " *" \n
  ("Param: " " * @param " str \n)
  " * @return " (skeleton-read "Returns: ") \n
  " */"
  @
  '(my/skeleton-indent-afterwards))
#+end_src

**** Config
#+begin_src emacs-lisp
(defun my/add-c-snippets (table)
  (my/add-fundamental-snippets table)
  (my/add-skeleton-abbrevs table '(("guard" . c-skeleton-guard)
                                   ("doxyheader" . c-skeleton-doxy-header)
                                   ("doxyfn" . c-skeleton-doxy-fn))))

;; For whatever reason, putting this in the use-package block doesn't immediately run
;; this when .c files are loaded, just when M-x c-ts-mode is executed
(add-to-list 'major-mode-remap-alist '(c-mode . c-ts-mode))
(use-package cc-mode
  :config
  (my/add-c-snippets c-mode-abbrev-table))

(use-package c-ts-mode
  :custom
  (c-ts-mode-indent-offset 4)
  (c-ts-mode-enable-doxygen t)
  :config
  (my/add-c-snippets c-ts-mode-abbrev-table))
#+end_src

*** Python
**** Snippets
***** Doc comment for functions
#+begin_src emacs-lisp
(define-skeleton python-skeleton-doc-func
  "Generate doc comment for function."
  ""
  "\"\"\"" (skeleton-read "Brief: ") \n
  \n
  (skeleton-read "Long: ") \n
  \n
  "Args:" \n
  ("Arg: " "    " str \n)
  \n
  "Returns:" \n
  "    " (skeleton-read "Returns: ") \n
  \n
  "Raises:" \n
  ("Exception: " "    " str \n)
  "\"\"\"")
#+end_src

***** Main function generator
#+begin_src emacs-lisp
(define-skeleton python-skeleton-main-fn
  "Generate main-func paradigm."
  ""
  "def main():" \n
  > _ \n
  > "pass" \n
  \n
  "if __name__ == '__main__':" \n
  > "main()")
#+end_src

**** Config
#+begin_src emacs-lisp
(defun my/add-python-snippets (table)
  (my/add-fundamental-snippets table)
  (my/add-skeleton-abbrevs table '(("dfunc" . python-skeleton-doc-func)
                                   ("mainf" . python-skeleton-main-fn))))

(add-to-list 'major-mode-remap-alist '(python-mode . python-ts-mode))
(use-package python
  :config
  (my/add-python-snippets python-mode-abbrev-table)
  (my/add-python-snippets python-ts-mode-abbrev-table))
#+end_src

*** JavaScript
#+begin_src emacs-lisp
(add-to-list 'major-mode-remap-alist '(javascript-mode . js-ts-mode))
(use-package js
  :config
  (my/add-fundamental-snippets js-mode-abbrev-table)
  (my/add-fundamental-snippets js-ts-mode-abbrev-table))
#+end_src

*** Fennel
#+begin_src emacs-lisp
(use-package fennel-mode
  :ensure t
  :mode ("\\.fnl\\'" . fennel-mode)
  :config
  (my/add-fundamental-snippets fennel-mode-abbrev-table))
#+end_src

*** Justfile
#+begin_src emacs-lisp
(use-package just-ts-mode
  :ensure t)
#+end_src

*** Go
#+begin_src emacs-lisp
(use-package go-ts-mode
  :defer 1)
#+end_src

*** Haskell
#+begin_src emacs-lisp
(use-package haskell-mode
  :ensure t)
#+end_src

*** Swift
#+begin_src emacs-lisp
(use-package swift-mode
  :ensure t
  :config
  (with-eval-after-load 'eglot
    (add-to-list 'eglot-server-programs
                 '(swift-mode . ("sourcekit-lsp")))))
#+end_src

*** Ada
#+begin_src emacs-lisp
(use-package ada-ts-mode
  :ensure t)
#+end_src

*** HTML
#+begin_src emacs-lisp
(add-to-list 'major-mode-remap-alist '(html-mode . html-ts-mode))
#+end_src

*** ESS
#+begin_src emacs-lisp
(use-package ess
  :ensure t)
#+end_src

*** Emacs Lisp
#+begin_src emacs-lisp
(use-package elisp-mode
  :custom
  (elisp-fontify-semantically t))
#+end_src

** Completion
Vertico, Marginalia, Consult, and Orderless for the minibuffer completion system
# TODO: embark
#+begin_src emacs-lisp
(use-package vertico
  :ensure t
  :custom
  (vertico-cycle t)
  (vertico-mode t))

(use-package vertico-posframe
  :ensure t
  :after vertico
  :custom
  (vertico-posframe-mode 1))

(use-package marginalia
  :ensure t
  :custom
  (marginalia-mode 1))

(use-package orderless
  :ensure t
  :custom
  (completion-styles '(orderless basic))
  (completion-category-overrides '((file (styles basic partial-completion)))))

(use-package savehist
  :custom
  (savehist-mode 1))

(use-package consult
  :ensure t
  :custom
  (consult-async-refresh-delay 0)
  (consult-async-min-input 1)
  :bind (("C-x p f" . consult-fd)
         ("C-x p g" . consult-ripgrep)
         ("C-x b" . consult-buffer)
         ("C-x p b" . consult-project-buffer)
         ("M-g g" . consult-goto-line)
         ("M-y" . consult-yank-pop)))
#+end_src

Corfu and completion preview for in-buffer completion, with kind-icon for svgs.
#+begin_src emacs-lisp
(use-package cape
  :ensure t
  :after corfu
  :init
  (defvar my/eglot-capf (cape-capf-super #'eglot-completion-at-point #'cape-abbrev))
  (defvar my/elisp-capf (cape-capf-super #'cape-abbrev #'cape-dabbrev #'elisp-completion-at-point))
  (defvar my/org-capf (cape-capf-super #'cape-abbrev #'cape-elisp-block))
  (defvar my/generic-capf (cape-capf-super #'cape-abbrev))

  (defun my/cape-capf-set ()
    (interactive)
    (setq-local completion-at-point-functions
                (list #'cape-file
                 (cond ((equal major-mode #'org-mode) my/org-capf)
                       ((or (equal major-mode #'emacs-lisp-mode)
                            (equal major-mode #'lisp-interaction-mode)) my/elisp-capf)
                       ((and (fboundp 'eglot-managed-p) (eglot-managed-p)) my/eglot-capf)
                       (t my/generic-capf)))))
  :hook (after-change-major-mode . my/cape-capf-set))

(use-package corfu
  :ensure t
  :bind ( :map corfu-mode-map
	  ("S-<tab>" . completion-at-point)
	  ("<backtab>" . completion-at-point))
  :custom
  (global-corfu-mode t)
  (corfu-cycle t)
  (corfu-popupinfo-delay '(0.5 . 0.5))
  :hook
  (prog-mode . corfu-mode)
  (corfu-mode . corfu-popupinfo-mode))

;; :TODO: fix suggestion in org mode at least not being anything other than a simple dict autocomplete (abbrev not showing?)
(use-package completion-preview
  :diminish completion-preview-mode
  :hook ((corfu-mode . completion-preview-mode)
         (org-mode . my/completion-preview-add-org))
  :custom (completion-preview-minimum-symbol-length 2)
  :init
  (defun my/completion-preview-add-org ()
    (setq-local completion-preview-commands
                (append completion-preview-commands '(org-self-insert-command org-delete-backward-char)))))

(use-package kind-icon
  :ensure t
  :after corfu
  :custom
  (corfu-margin-formatters (list #'kind-icon-margin-formatter)))
#+end_src

Auto-completion of matching parenthesis is really useful, although it can get in the way sometimes (ie. TODO fix this or not in all prog-modes)
#+begin_src emacs-lisp
(use-package elec-pair
  :functions electric-pair-default-inhibit
  :custom
  (electric-pair-mode t))
#+end_src

Avy to help with jumping around text
#+begin_src emacs-lisp
(use-package avy
  :ensure t
  :bind (("M-j" . avy-goto-char-timer)))
#+end_src

eldoc for inline documentation
#+begin_src emacs-lisp
(use-package eldoc
  :diminish eldoc-mode)
(use-package eldoc-box
  :ensure t
  :after eglot
  :bind (:map eglot-mode-map ("C-c C-e" . 'eldoc-box-help-at-point)))
#+end_src

** Apps
*** EMMS
#+begin_src emacs-lisp
(defvar my/radio-channel-location "~/.config/sops-nix/secrets/emacs-radio-channels.el")

(defun my/radio-play ()
  (interactive)
  (let* ((my/radio-channels (my/user-secret-else my/radio-channel-location nil))
         (choice (completing-read "Station:" (seq-map (lambda (pair) (car pair)) my/radio-channels)))
         (association (assoc choice my/radio-channels)))
    (message "playing %s" choice)
    (emms-play-url (if association
                       (cdr association)
                     choice))))

(use-package emms
  :ensure t
  :bind (("C-x C-a p e" . emms)
         ("C-x C-a p p" . emms-start)
         ("C-x C-a p s" . emms-stop)
         ("C-x C-a p r" . my/radio-play))
  :custom
  (emms-player-list '(emms-player-mpv))
  :config
  (require 'emms-setup)
  (emms-minimalistic))
#+end_src

*** Yeetube
#+begin_src emacs-lisp
(defun my/get-channel-id (url)
  (with-current-buffer (url-retrieve-synchronously url)
    (search-forward "channelId")
    (search-forward "\"")
    (search-forward "\"")
    (set-mark (point))
    (search-forward "\"")
    (backward-char)
    (buffer-substring (region-beginning) (region-end))))

(defun my/yeetube-channel-rss-at-point ()
  (interactive)
  (search-forward "ago")
  (let ((url (format "www.youtube.com/feeds/videos.xml?channel_id=%s" (my/get-channel-id (yeetube-get-url)))))
    (when (y-or-n-p (format "Copy to kill ring? %s: " url))
      (kill-new url))))

(use-package yeetube
  :ensure t
  :bind (("C-x C-a y" . yeetube-search)
         :map yeetube-mode-map
         ("C-c C-c" . my/yeetube-channel-rss-at-point))
  :functions yeetube-get-url
  :custom
  (yeetube-mpv-additional-flags '("--hwdec=yes"))
  :config
  (setq yeetube-mpv-video-quality "1080"))
#+end_src

*** Dired
#+begin_src emacs-lisp
(use-package vscode-icon
  :ensure t)

(use-package dired
  :custom (dired-use-ls-dired (not (eq system-type 'darwin))))

(use-package dired-sidebar
  :ensure t
  :after vscode-icon
  :bind ("C-x C-d" . dired-sidebar-toggle-sidebar)
  :hook (dired-sidebar-mode . (lambda ()
                                (unless (file-remote-p default-directory)
                                  (auto-revert-mode))))
  :custom
  (dired-sidebar-theme 'vscode))

(use-package dired-preview
  :ensure t
  :custom
  (dired-preview-global-mode t)
  (dired-preview-delay 0.1))
#+end_src

*** PDF-Tools
Improvements over the stock docview
#+begin_src emacs-lisp
(use-package pdf-tools
  :ensure t
  :config
  (pdf-tools-install nil t))
  #+end_src

*** Page View
View org documents as a series of pages
#+begin_src emacs-lisp
(use-package page-view)
#+end_src

*** Eat
If Eat isn't working on macOS (as in, the formatting is all off), you probably need to run =M-x eat-compile-terminfo=. I could not figure out how to get that to work as part of a nix derivation, maybe once macOS switces to ncurses >= 6.2?
#+begin_src emacs-lisp
(use-package eat
  :ensure t)
#+end_src

I wrote a custom =toggleterm= package that can popup and hide an eat buffer (could be configured for other shells)
#+begin_src emacs-lisp
(use-package toggleterm
  :defer nil
  :bind ("C-x C-a t" . toggleterm-dwim))
#+end_src

*** Gnus
I use the builtin Gnus for RSS feeds, but I will probably expand to email as well at some point
#+begin_src emacs-lisp
(use-package gnus
  :defines
  gnus-newsrc-alist
  gnus-tmp-group
  :functions
  gnus-collect-urls
  gnus-browse-exit
  gnus-browse-toggle-subscription-at-point
  gnus-browse-foreign-server)

(defun my/subscribe-feed-url (method url)
  (gnus-browse-foreign-server (list method url))
  (with-current-buffer "*Gnus Browse Server*"
    (let ((colon (string-match ":" (buffer-string))))
      (message "%s" (buffer-string))
      (gnus-browse-toggle-subscription-at-point colon))
    (gnus-browse-exit)))

(defvar my/feeds-file "~/.config/sops-nix/secrets/emacs-feeds.el")

(defun my/newsrc-contains (method+url)
  (seq-filter (lambda (entry)
                (string-prefix-p method+url (nth 0 entry)))
              gnus-newsrc-alist))

(defun my/add-missing-feeds ()
  (dolist (feed (my/user-secret-else my/feeds-file nil))
    (let* ((method+url (nth 0 feed))
           (split (string-split method+url "+"))
           (method (read (car split)))
           (url (cl-second split)))
      (unless (my/newsrc-contains method+url)
        (message "adding feed %s" method+url)
        (my/subscribe-feed-url method url)))))

;; I don't really like the qualified name,
;; and the non-qualified name isn't as
;; easily customizable
(defun gnus-user-format-function-G (_)
  (let* ((url-and-method (nth 0 (string-split gnus-tmp-group ":")))
         (mapped (assoc url-and-method (my/user-secret-else my/feeds-file nil))))
    (if (null mapped)
        gnus-tmp-group
      (cdr mapped))))

(defun my/select-and-play-url ()
  (interactive)
  (let ((urls (gnus-collect-urls)))
    (cond ((length< urls 1) (message "no URLs found"))
          ((length= urls 1) (emms-play-url (car urls)))
          ((length> urls 1) (emms-play-url (completing-read "URLs: " urls))))))

(defun my/rss-youtube-play ()
  (interactive)
  (require 'yeetube)
  (string-match "<yt:video:[[:alnum:]]\\([[:alnum:]]*@\\)[[:alnum:]]*.nnatom>" (buffer-string))
  (apply yeetube-play-function (format "https://www.youtube.com/watch?v=%s" (match-string 1)) nil))

(use-package gnus
  :custom
  (gnus-group-line-format "%M%S%p%P%5y:%B%(%uG%)\n")
  (gnus-parameters '(("\\(nnnrss\\|nnatom\\)+.*"
                      (display . all)
                      (gnus-article-sort-functions '((not gnus-article-sort-by-date)))
                      (gnus-show-threads nil))))
  (gnus-permanently-visible-groups "\\(nnnrss\\|nnatom\\)+.*")
  :bind (("C-x C-a g" . gnus)
         :map gnus-article-mode-map
         ("C-c C-p" . my/select-and-play-url)
         ("C-c C-y" . my/rss-youtube-play))
  :hook (gnus-setup-news . my/add-missing-feeds))
#+end_src

** Miscellaneous
Remove the annoying files Emacs dumps all over the system.
Also, I've been burned one too many times by accidentally opening a massive file with =font-lock=
#+begin_src emacs-lisp
(defun my/find-file-massive-basic ()
  "If a file is large, remove features to not freeze."
  (when (and (> (buffer-size) (* 256 1024)) (derived-mode-p 'text-mode))
    (setq buffer-read-only t)
    (buffer-disable-undo)
    (text-mode)))
(use-package files
  :hook (find-file . my/find-file-massive-basic)
  :custom
  (backup-directory-alist `(("." . ,(concat user-emacs-directory "backups"))))
  (create-lockfiles nil)
  (save-abbrevs nil))
#+end_src

Visual Undo to visualize the undo tree
#+begin_src emacs-lisp
(use-package vundo
  :ensure t
  :bind ("C-?" . vundo))

(use-package undo-fu-session
  :ensure t
  :custom
  (undo-fu-session-global-mode t))
#+end_src

Always use utf-8 when possible. Also, move to trash rather than deleting
#+begin_src emacs-lisp
(use-package emacs
  :config
  (set-default-coding-systems 'utf-8)
  :custom (delete-by-moving-to-trash t))
#+end_src

IBuffer for improved buffer management
#+begin_src emacs-lisp
(use-package ibuffer
  :bind ("C-x C-b" . ibuffer)
  :custom (ibuffer-auto-mode t))

(use-package ibuffer-vc
  :ensure t
  :bind (:map ibuffer-mode-map ("/ V" . ibuffer-vc-set-filter-groups-by-vc-root)))
#+end_src

Never use tabs; always spaces. Also, always force DWIM if possible
#+begin_src emacs-lisp
(use-package simple
  :custom
  (indent-tabs-mode nil)
  (kill-region-dwim 'emacs-word)
  :bind
  ("M-u" . upcase-dwim)
  ("M-l" . downcase-dwim)
  ("M-c" . capitalize-dwim))
#+end_src

Delete the region if selected when typing
#+begin_src emacs-lisp
(use-package delsel
  :custom (delete-selection-mode t))
#+end_src

Indent guides to check indentation
#+begin_src emacs-lisp
(use-package indent-bars
  :ensure t
  :hook (prog-mode . indent-bars-mode)
  :custom
  (indent-bars-ts-support t)
  (indent-bars-color '(highlight :blend 0.6))
  (indent-bars-highlight-current-depth '(:face default :blend 1.0))
  (indent-bars-treesitter-scope '((python function_definition class_definition for_statement if_statement with_statement while_statement))))
#+end_src

#+begin_src emacs-lisp
(use-package sqlite-mode
  :bind
  ( :map sqlite-mode-map
    ("n" . next-line)
    ("p" . previous-line)))
#+end_src

#+begin_src emacs-lisp
(use-package hideshow
  :diminish hs-minor-mode
  :hook (prog-mode . hs-minor-mode)
  :custom (hs-show-indicators t))
#+end_src

#+begin_src emacs-lisp
(use-package project
  :custom
  (project-mode-line t)
  (project-mode-line-format '(" [" (:eval (if (project-current) (substring (project-mode-line-format) 1) "")) "]")))
#+end_src

*** Version Control
#+begin_src emacs-lisp
(use-package vc-git
  :config
  ;; I really just need to be able to use ¯\_(ツ)_/¯
  (my/add-fundamental-snippets vc-git-log-edit-mode-abbrev-table))

(use-package vc-jj
  :ensure t)
#+end_src

*** Recentf
Keep track of recently opened files
#+begin_src emacs-lisp
(use-package recentf
  :custom (recentf-mode t)
  :bind ("C-x C-r" . recentf))
#+end_src

Return to the same spot when opening again
#+begin_src emacs-lisp
(use-package saveplace
  :custom
  (save-place-mode t)
  (save-place-autosave-interval 3.0))
#+end_src

*** exec-path-from-shell
This fixes the path for executables installed with nix, etc.
Only needed on macOS and linux, or if launched as a daemon
#+begin_src emacs-lisp
(use-package exec-path-from-shell
  :ensure t
  :custom
  (exec-path-from-shell-arguments nil)
  :config
  ;; This way we can preserve our linkage to e.g. VLC
  (let ((nix-store-exec-path (seq-filter (lambda (item) (string-prefix-p "/nix/store" item)) exec-path)))
    (exec-path-from-shell-initialize)
    (setq exec-path (append nix-store-exec-path exec-path))
    (setenv "PATH" (string-join (cons (getenv "PATH") nix-store-exec-path) ":"))))
#+end_src

*** Outline modes
#+begin_src emacs-lisp
(use-package outline
  :diminish (outline-minor-mode)
  :hook (prog-mode . outline-minor-mode)
  :bind ("C-<iso-lefttab>" . outline-cycle))
#+end_src

*** Compile-mode
#+begin_src emacs-lisp
(use-package ansi-color
  :functions
  my/colorize-buffer
  ansi-color-apply-on-region
  :init
  (defun my/colorize-buffer ()
    (let ((inhibit-read-only t))
      (ansi-color-apply-on-region (point-min) (point-max)))))

(use-package compile
  :hook (compilation-filter . my/colorize-buffer)
  :custom
  (compilation-scroll-output t))
#+end_src

*** Show-paren-mode
#+begin_src emacs-lisp
(use-package paren
  :custom
  (show-paren-mode t)
  (show-paren-style 'expression))
#+end_src

*** Mouse
#+begin_src emacs-lisp
(use-package mwheel
  :custom
  (mouse-wheel-tilt-scroll t)
  (mouse-wheel-flip-direction t))
#+end_src

*** EditorConfig
#+begin_src emacs-lisp
(use-package editorconfig
  :custom (editorconfig-mode t)
  :diminish editorconfig-mode)
#+end_src

*** Ace Window
better window navigation
#+begin_src emacs-lisp
(use-package ace-window
  :ensure t
  :bind
  ("M-o" . ace-window))
#+end_src

* Direnv
Apparently this is supposed to be the last thing ever hooked to ensure proper functioning
#+begin_src emacs-lisp
(use-package envrc
  :ensure t
  :after exec-path-from-shell
  :hook (after-init . envrc-global-mode)
  :functions envrc-propagate-environment
  :config
  (advice-add 'Man-completion-table :around #'envrc-propagate-environment)
  (advice-add 'sql-sqlite :around #'envrc-propagate-environment))
#+end_src

Also theming is borked if loaded before the end
#+begin_src emacs-lisp
;; (load-theme my/theme t)
(ef-themes-load-theme 'ef-elea-dark)
#+end_src
# TODO: treesit-utils, other prog modes, custom modeline, golden-ratio, dimmer, gptel/claude-code-ide/aidermacs
